#!/usr/bin/env zsh
# ===============================================
# zsh loads this file only if it is a login-shell
# ===============================================

# load machine local profile
[[ -f $ZDOTDIR/.zprofile.local ]] && source $ZDOTDIR/.zprofile.local

# start graphical session
if systemctl -q is-active graphical.target && [[ -z $DISPLAY && $XDG_VTNR -eq 1 && ( -z $XDG_SESSION_TYPE || $XDG_SESSION_TYPE == tty ) && $OSNAME == "Arch" ]]; then
  echo -n "Select WM/DE [bspwm, gnome(-x11), i3, kde, lxqt, sway] [default: sway]:\n>>> "
  read wm
  if [[ -z $wm ]]; then
    wm="sway"
  fi

  if [[ $wm == "exit" ]]; then
    exit 0
  fi

  if [[ $wm == (sway|gnome) ]]; then  # for wayland
    # set xdg env var
    export XDG_SESSION_TYPE=wayland

    # set qt
    export QT_QPA_PLATFORM=wayland-egl
    export QT_QPA_PLATFORMTHEME=qt5ct

    # set IM
    export XMODIFIERS=@im=fcitx
    export GTK_IM_MODULE=fcitx
    export QT_IM_MODULE=fcitx
    export GLFW_IM_MODULE=fcitx

    # set firefox
    export MOZ_ENABLE_WAYLAND=1

    case $wm in
      sway)
        # set env vars for sway
        export XDG_CURRENT_DESKTOP=sway
        systemctl --user set-environment XDG_CURRENT_DESKTOP=sway

        # use vulkan renderer
        export WLR_REDERER=vulkan

        # start sway
        exec sway ;;
      gnome)
        # start gnome wayland session
        exec dbus-run-session gnome-session ;;
    esac
    exit 0

  else
    # start X session
    exec startx ~/.xinitrc $wm -- -maxclients 2048
  fi
fi

# vim: sw=2 et
